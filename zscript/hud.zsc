class MyStandaloneGameHUD : BaseStatusBar
{
	HUDFont mHUDFont;
	MyStandaloneGamePlayer pmo;

	override void Init(void)
	{
		Super.Init();
		SetSize(0, 320, 200);
		Font fnt = SmallFont;
		mHUDFont = HUDFont.Create(fnt, 0, Mono_Off, 1, 1);
	}

	override void Draw(int state, double TicFrac)
	{
		if (CPlayer && CPlayer.mo)
		{
			pmo = MyStandaloneGamePlayer(CPlayer.mo);
		}

		BaseStatusBar.Draw(state, TicFrac);

		if (state == HUD_StatusBar || state == HUD_Fullscreen)
		{
			BeginHUD(forcescaled: true);

			DrawHealth();
		}
	}

	void DrawHealth(void)
	{
		int hpcolor = Font.CR_UNTRANSLATED;
		if (pmo.player.health < 25) {hpcolor = Font.CR_BRICK;}
		else if (pmo.player.health < 50) {hpcolor = Font.CR_GOLD;}
		DrawString(mHUDFont, StringTable.Localize("$TXT_HUD_HEALTH") .. FormatNumber(pmo.player.health, 3), (-20, -48), DI_SCREEN_RIGHT_BOTTOM | DI_TEXT_ALIGN_RIGHT, hpcolor);
		
		int oxcolor = Font.CR_UNTRANSLATED;
		if (pmo.CountInv("Oxygen") < 25) {oxcolor = Font.CR_BRICK;}
		else if (pmo.CountInv("Oxygen") < 50) {oxcolor = Font.CR_GOLD;}
		DrawString(mHUDFont, StringTable.Localize("$TXT_HUD_OXYGEN") .. FormatNumber(pmo.CountInv("Oxygen")), (-20, -40), DI_SCREEN_RIGHT_BOTTOM | DI_TEXT_ALIGN_RIGHT, oxcolor);
		
		DrawString(mHUDFont, StringTable.Localize("$TXT_HUD_METAL") .. FormatNumber(pmo.CountInv("Metal")), (-20, -28), DI_SCREEN_RIGHT_BOTTOM | DI_TEXT_ALIGN_RIGHT);
		
		int fuelcolor = Font.CR_UNTRANSLATED;
		if (pmo.fuellevel > 0) {fuelcolor = Font.CR_GOLD;}
		if (pmo.fuellevel >= 350) 
		{
			fuelcolor = Font.CR_GREEN;
			DrawString(mHUDFont, StringTable.Localize("$FUEL_MAX"), (-20, -12), DI_SCREEN_RIGHT_BOTTOM | DI_TEXT_ALIGN_RIGHT, fuelcolor, 0.5, scale: (0.5, 0.5));
		}
		DrawString(mHUDFont, StringTable.Localize("$TXT_HUD_FUEL") .. FormatNumber(pmo.fuellevel / 3.5), (-20, -20), DI_SCREEN_RIGHT_BOTTOM | DI_TEXT_ALIGN_RIGHT, fuelcolor);
		
	}
}
